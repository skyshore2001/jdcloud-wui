#!perl
=pod
/**
@module webcc-merge js文件合并

根据指定标记，将html文件中引入的js文件合并成一个文件。

示例：假如index.html中包含下面内容

	<!-- WEBCC_BEGIN MERGE -->
	<script src="../src/doc.js"></script>
	<script src="../src/common.js"></script>
	<script src="../src/commonjq.js"></script>
	<script src="../src/app.js"></script>
	<script src="../src/callSvr.js"></script>
	<script src="../src/mui-showPage.js"></script>
	<script src="../src/mui.js"></script>
	<script src="../src/initPageList.js"></script>
	<script src="../src/initPageDetail.js"></script>
	<script src="../src/mui-name.js"></script>
	<!-- WEBCC_END -->

运行

	perl webcc_merge.pl index.html > jdcloud-mui.js

在"WEBCC_BEGIN MERGE"和"WEBCC_END"之间引入的js文件会被拼合成一个js文件并输出。

如果要在输出文件的首行添加注释，可以使用P_CMT环境变量，如

	P_CMT="jdcloud-mui 1.0" perl webcc_merge.pl ...

注意：

- 支持script标签被注释掉（用html注释即"<!--"和"-->"），这样就不会被合并到最终的文件中。
*/
=cut

use strict;
use warnings;
use File::Basename;

my $file=$ARGV[0] || do {
	print "Usage: webcc_merge {html-file} > {merged file}\n";
	exit;
};

my $dir=dirname($file);

undef $/;
$_ = <>;

if (! /WEBCC_BEGIN \s+ MERGE .*?$ (.*) ^.*WEBCC_END/xms) {
	die("cannot find WEBCC tag in file");
}
$_ = $1;
s/<!--.*?-->//gs;

chdir($dir);
print "// $ENV{P_CMT}\n" if $ENV{P_CMT};
my @files = /src="(.*?)"/g;
for (@files) {
	outputOne($_);
}
print "// Generated by webcc_merge\n";
print "// vi: foldmethod=marker\n";

sub outputOne # ($f)
{
	my $f = $_[0];
	my $name = basename($f);
	local $/;
	open IN, $f;
	print "// ====== WEBCC_BEGIN_FILE $name {{{\n";
	local $_ = <IN>;
	print $_;
	print "// ====== WEBCC_END_FILE $name }}}\n\n";
	close IN;
}
